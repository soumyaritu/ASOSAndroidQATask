/*
 * By Martin Chamarro for ASOS
 * Copyright 2018 Martin Chamarro (@martinchamarro)
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply from: 'tests.gradle'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.asos.recipes"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            // Default config
        }
        release {
            // Default config
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug-build' + getDate()
            debuggable true
            buildConfigField "boolean", "DEBUG_MODE", "true"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            buildConfigField "boolean", "DEBUG_MODE", "false"
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dependencies {
        implementation rootProject.ext.dependencies.support
        implementation rootProject.ext.dependencies.design
        implementation rootProject.ext.dependencies.cardView
        implementation rootProject.ext.dependencies.constraintLayout
        implementation rootProject.ext.dependencies.gson
        implementation rootProject.ext.dependencies.dagger
        kapt rootProject.ext.dependencies.daggerCompiler
        implementation rootProject.ext.dependencies.retrofit
        implementation rootProject.ext.dependencies.retrofitGson
        implementation rootProject.ext.dependencies.okhttp3Logging
        implementation rootProject.ext.dependencies.kotlin
        implementation rootProject.ext.dependencies.picasso
        implementation rootProject.ext.dependencies.picassoOkhttp
        implementation rootProject.ext.dependencies.rxAndroid
        implementation rootProject.ext.dependencies.rxJava
        implementation rootProject.ext.dependencies.roundedImageView

        // Unit testing
        testImplementation rootProject.ext.testingDependencies.junit
        testImplementation rootProject.ext.testingDependencies.mockitoCore
        testImplementation(rootProject.ext.testingDependencies.mockitoKotlin) {
            exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
        }
        testImplementation rootProject.ext.testingDependencies.robolectric

        // Instrumentation testing
        androidTestImplementation(rootProject.ext.testingDependencies.espressoCore) {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        androidTestImplementation(rootProject.ext.testingDependencies.espressoIntents) {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        androidTestImplementation(rootProject.ext.testingDependencies.espressoContrib) {
            // Necessary to avoid version conflicts
            exclude group: 'com.android.support', module: 'appcompat'
            exclude group: 'com.android.support', module: 'support-v4'
            exclude group: 'com.android.support', module: 'support-annotations'
            exclude module: 'recyclerview-v7'
        }
        androidTestImplementation(rootProject.ext.testingDependencies.testRunner) {
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        androidTestImplementation rootProject.ext.testingDependencies.hamcrest
    }
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}